///////////////////////////////////////
//
//	Computer Graphics TSBK03
//	Conrad Wahlén - conwa099
//
///////////////////////////////////////

#version 430

layout(local_size_x = 64) in;

struct Bin {
	uint bins;
	uint totalBins;
	float binSize;
	float areaSize;
};

layout (std430, binding = 3) buffer BinCounterBuffer {
	uint binCounter[];
};

layout (std430, binding = 4) buffer PrefixCounterBuffer {
	uint prefixCounter[];
};

layout (std140, binding = 11) uniform BinBuffer {
	Bin binInfo;
};

shared uint sData[gl_WorkGroupSize.x];
														
void main(void) {
	uint ltid = gl_LocalInvocationIndex.x;
	uint gtid = gl_GlobalInvocationID.x;
	
	sData[ltid] = binCounter[gtid];

	barrier();

	for(int s = 1; s < gl_WorkGroupSize.x; s *= 2) {
		if (ltid % (2*s) == 0) {
			sData[ltid] += sData[ltid + s];	
		}
		barrier();
	}


	if (ltid == 0) prefixCounter[0] = sData[0];
}

