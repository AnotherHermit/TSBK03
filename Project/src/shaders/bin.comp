#version 430

layout(local_size_x = 64) in;

struct Particle {
	vec3 position;
	uint bin;
	vec3 velocity;
	uint padding;
};

layout (std140, binding = 0) buffer ParticleBuffer {
	Particle data[];
} particles;

layout (binding = 3) buffer BinCounterBuffer {
	uint data[];
} binCounter;

uniform uint numBins;
uniform float binSize;
																
void main(void) {
	uint binsPerSide = numBins / 2;

	uint currentParticle = gl_GlobalInvocationID.x;
	
	vec3 pos = particles.data[currentParticle].position;

	// Calculate bin
	vec3 pos2id = min(max(floor(pos / binSize), -float(binsPerSide)), float(binsPerSide) - 1) + binsPerSide;

	uint xID = uint(pos2id.x);
	uint yID = uint(pos2id.y);
	uint zID = uint(pos2id.z);
	uint binID = xID + numBins * yID + numBins * numBins * zID;
	particles.data[currentParticle].bin = binID;
	atomicAdd(binCounter.data[binID], 1);
}
